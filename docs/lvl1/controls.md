# Control structures - if, try, for, while, until

Control structures allow branching and repeating execution of code. Imba has
several different control structures.

- `if` blocks control branching
- `try` blocks handle exceptions
- loops (`for`, `while`, `until`) allow repetitive execution of code blocks

## Cheats

- Imba has the `if`, `try`, `while`, `until` and `for` blocks.
- There are no parentheses around conditions.
- There is no punctuation in blocks (e.g., `if x`).
- `if` block can have `else if` and `else` branches.
- `for`, `white`, `until` can be passed and assigned as arrays.
- `for`, `white`, `until` can `break` and `continue`.
- `for x in arr` or `for x, idx in arr` to loop over arrays.
- `for key of obj` or `for key, val of obj` to loop over object properties.

## The `if` block

Sometimes we want to do different things depending on some condition. For
instance, 'if user is logged in, show them the profile page, and otherwise
take them to the log in page.'

Just like the name suggests, the `if` blocks are only evaluated if some
condition is met. The condition can be any valid Imba expression, including any
of the control blocks found in this section (yes, `if` block itself, too!).

For example:

```imba
var x = -2

if x < 0
    x = -x  # we only reach this part of the code if `x` is less than 0
```

We can also specify what happens in the other case:

```imba
var x = -2

if x < 0
    x = -x + 1
else
    x = x + 1
```

In the last example, we repeat `+ 1` twice. Since the `if` block is an 
expression, we can simplify this:

```imba
var x = -2

x = (
    if x < 0
        -x
    else
        x
) + 1
```

We have wrapped the entire `if` block in parentheses to separate and added `1`
to it. Newline isn't really required after the opening parenthesis, but we've
added it to make the code a bit more readable.

There is an even shorter way to write the last example:

```imba
var x = -2

x = (if x < 0 then -x else x) + 1
```

Notice the inclusion of the `then` keyword.

Although the last example does look like an `if` block, it's not a block but a
ternary expression.

The `if` block is not restricted to just `if` and `else` branches. One or
more additional conditions can be specified as `else if` branches. For
example:

```imba
var x = -2

if x < 0
    x = -x
else if x === 0
    x = 1
else
    x = x * 2
```

The `else if` branches cannot be used in ternary expressions.

## The `try` block

Perfectly valid code will sometimes throw exceptions. Exceptions are objects
generated by some error condition or thrown by some code. Exception objects
will propagate through the application from the point where it was thrown,
then back up through the path that lead to the point. If it is not caught
somewhere on the path, it will eventually reach the JavaScript engine itself,
and be treated as an 'uncaught' exception. (This is usually going to show up
as a traceback in the developer console or in the terminal.)

We can use exceptions either to manage error conditions, or to send messages
through our application by (ab)using the fact that exception will travel
until caught. Here we will not dwell too much on how exceptions are used, but 
instead simply look at how `try` blocks are used to deal with them.


